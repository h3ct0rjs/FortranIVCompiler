C -------------------------------------------------------   
C COMPUTE THE CRITICAL VALUES FOR A QUADRATIC EQN
C 0=A*X**2+B*X+C
C RETURNS DISCRIMINANT, ROOTS, VERTEX, FOCAL LENGTH, FOCAL POINT
C X1 AND X2 ARE THE ROOTS
C-------------------------------------------------------   
      SUBROUTINE QUADR(A,B,C,DISCR,X1,X2,VX,VY,FL,FPY)
      REAL A,B,C,DISCR,X1,X2,VX,VY,FL,FPY

C DISCRIMINANT, VERTEX, FOCAL LENGTH, FOCAL POINT Y
      DISCR = B**2.0 - 4.0*A*C
      VX = -B / (2.0*A)
      VY = A*VX**2.0 + B*VX + C
      FL = 1.0 / (A * 4.0)
      FPY = VY + FL
      FL = ABS(FL)

C COMPUTE THE ROOTS BASED ON THE DISCRIMINANT
      IF(DISCR) 110,120,130

C -VE DISCRIMINANT, TWO COMPLEX ROOTS, REAL=X1, IMG=+/-X2
110   X1 = -B / (2.0*A)
      X2 = SQRT(-DISCR) / (2.0*A)
      RETURN
       
C ZERO DISCRIMINANT, ONE REAL ROOT
120   X1 = -B / (2.0*A)
      X2 = X1
      RETURN 

C +VE DISCRIMINANT, TWO REAL ROOTS
130   X1 = (-B + SQRT(DISCR)) / (2.0*A)
      X2 = (-B - SQRT(DISCR)) / (2.0*A)
      RETURN
C
C NEXT STORE SUBROUTINE ON DISK USING DUP
      END
// DUP
*DELETE             QUADR
*STORE      WS  UA  QUADR
// JOB
// FOR
*LIST SOURCE PROGRAM   
*IOCS(CARD,1132 PRINTER)   
*ONE WORD INTEGERS   
C-------------------------------------------------------   
C PROCESS DATA CARDS WITH A,B,C
C UNTIL A=0
C-------------------------------------------------------   

      DATA ICARD,IPRT /2,3/
      REAL A,B,C
      REAL DISCR,XR1,XR2,VX,VY,FL,FPY

      WRITE(IPRT,901)
901   FORMAT(' ------------------------------------------------------')

C READ A B C, IF A=0 THEN EXIT
100   READ(ICARD,801)A,B,C
801   FORMAT(3F8.3)

C     EXIT WHEN A IS ZERO
      IF (A) 110,9000,110

C PRINT A B C 
110   WRITE(IPRT,902)A,B,C
902   FORMAT(' QUADRATIC A=',F8.3,' B=',F8.3,' C=',F8.3)

C COMPUTE AND PRINT THE CRITICAL VALUES
      CALL QUADR(A,B,C,DISCR,XR1,XR2,VX,VY,FL,FPY)
      WRITE(IPRT,903) DISCR
903   FORMAT(' DISCRIMINANT=',F9.4)
      WRITE(IPRT,904) VX,VY
904   FORMAT(' VERTEX X=',F9.4,'  Y=',F9.4)
      WRITE(IPRT,905) FL
905   FORMAT(' FOCAL LENGTH=',F9.4)
      WRITE(IPRT,906) VX,FPY
906   FORMAT(' FOCAL POINT X=',F9.4,'  Y='F9.4)

      IF (DISCR) 120,130,140

C -VE DISCRIMINANT, TWO COMPLEX ROOTS
120   WRITE(IPRT,913) XR1, XR2
913   FORMAT(' COMPLEX ROOTS =(',F9.4,'  +/-',F9.4,'I)')
      GO TO 200
       
C ZERO DISCRIMINANT, ONE REAL ROOT
130   WRITE(IPRT,912) XR1
912   FORMAT(' ROOT  X =',F9.4)
      GO TO 200

C +VE DISCRIMINANT, TWO REAL ROOTS
140   WRITE(IPRT,911) XR1, XR2
911   FORMAT(' ROOTS X1=',F9.4,'   X2=',F9.4)
C --- GO TO 200

C END OF QUAD
200   WRITE(IPRT,901)
      GO TO 100

C END OF PROGRAM
C DATA FOLLOWS XEQ CARD
