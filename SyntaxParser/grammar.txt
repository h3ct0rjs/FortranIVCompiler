# Gramatica para FORTRAN IV

program : program statement
        | statement
        ;

statement : INT command
        | command
        ;

command : variable ASSIGN expr
        | CALL callOption
        | CONTINUE
        | DATA dataOption
        | DIMMENSION dimmensionOption
        | DO doOption
        | END
        | FORMAT LPAREN formatOptions RPAREN
        | FUNCTION ID LPAREN varlist RPAREN
        | GOTO gotoOption
        | IF LPAREN relexpr RPAREN INT ',' INT ',' INT
        | INTEGER exprlist
        | PAUSE pauseOption
        | READ readOption
        | REAL exprlist
        | RETURN
        | SUBROUTINE ID LPAREN varlist RPAREN
        | STOP stopOption
        | WRITE LPAREN optionsIO RPAREN varlist
        ;

gotoOption: INT
        |ID
        | LPAREN intlist RPAREN ',' variable
        ;

doOption: INT variable ASSIGN INT ',' INT ',' INT
        | INT variable ASSIGN INT ',' INT
        ;

pauseOption: INT
        | empty
        ;

stopOption: INT
        | empty
        ;

callOption: ID LPAREN paramlist RPAREN
        | EXIT callExit
        ;

callExit: LPAREN INT RPAREN
        | empty
        ;

readOption: LPAREN optionsIO RPAREN varlist
        | LPAREN optionsIO RPAREN
        ;

expr    : expr PLUS expr
        | expr MINUS expr
        | expr TIMES expr
        | expr DIVIDE expr
        | expr EXPONENT expr
        | MINUS expr
        | LPAREN expr RPAREN
        | INT
        | RREAL
        | variable
        ;


exprlist: exprlist ',' expr
        | expr
        ;

relexpr : expr LT expr
        | expr LE expr
        | expr GT expr
        | expr GE expr
        | expr EQ expr
        | expr NE expr
        | relexpr AND relexpr
        | relexpr OR  relexpr
        | NOT relexpr
        ;

variable: ID LPAREN expr ',' expr RPAREN
        | ID LPAREN expr RPAREN
        | ID
        ;

varlist : varlist ',' variable
        | variable
        ;

paramlist : ID ',' paramlist
        | ID

number  : INT
        | RREAL
        | MINUS INT
        | MINUS RREAL
        ;

intlist : intlist ',' INT
        | INT
        ;

optionsIO: INT ',' INT
        | INT "'" INT
        | ID ',' INT
        | ID "'" INT
        | INT ',' '*'
        | INT "'" '*'
        ;

-----------------------------------------------------------------
formatOptions: formatOptions ',' formatOptions
        | formatOptions '/' formatOptions
        | LPAREN formatOptions RPAREN
        | INT conversion
        | conversion
        | string
        | empty
        ;

conversion: LPAREN conversion RPAREN
        | conversion ',' typeconversion
        | typeconversion
        ;

typeconversion: INT ID
        | ID INT '.' INT
        | ID INT
        | ID string
        ;
------------------------------------------------------------------

dimmensionOption: dimmensionOption ',' ID LPAREN intlist RPAREN
        | ID LPAREN intlist RPAREN
        ;

dataOption: varlist ‘/’ datalist ‘/‘ ‘,’ dataOption
        | varlist ‘/‘ datalist ‘/‘
        ;

datalist: datalist ‘,’ INT ‘*’ number
        | datalist ‘,’ number
        | INT ‘*’ number
        | number
        ;
